
model
{
  ###################################
  ########### PROCESS MODEL #########
  ###################################

  for(i in 1:N){ # iterate over all years and populations
    fry[i] ~ dlnorm(log(prod[stock[i]]*spawners[i])+yearEffect[year[i]], SRresidTau[stock[i]])
    fryRes[i] <- fry[i]/(1/pLeave^3 + fry[i]^3/cap[stock[i]]^3)^(1/3)
    fryMig[i] <- fry[i]-fryRes[i]
    smoltRes[i] <- fryRes[i]*winterSurv[i]
    smoltMig[i] <- fryMig[i]*winterSurv[i]*fryAdj[stock[i]]
    smolt[i] <- smoltRes[i] + smoltMig[i]

    escapement[i] <- smolt[i] * oceanSurv[i] * (1 - HR[i])
  
    logit(oceanSurv[i]) <- oceanSurvL[i] + yearEffectOS[year[i]]
    logit(winterSurv[i]) <- winterSurvL[i]
    oceanSurvL[i] ~ dnorm(oceanSurvPopL[stock[i]], oceanSurvPopTau[stock[i]])
    winterSurvL[i] ~ dnorm(winterSurvPopL[stock[i]], winterSurvPopTau[stock[i]])
  }

  # offset escapement to produce spawners
  # spawners are in calendar years and escapement are in brood years
  for(pop in 1:Npops){
    for(i in (escStartInd[pop]+3):escStopInd[pop]){
      spawnersWild[i] <- escapement[i-3]
      spawners[i] <- spawnersWild[i] / (1-pHOS[i])
    }
    # fill in the missing years with a vague prior
    for(i in escStartInd[pop]:(escStartInd[pop]+2)){
      spawnersWild[i]  ~ dlnorm(0,0.0001)
      spawners[i] <- spawnersWild[i] / (1-pHOS[i])
    }
  }

  ### population specific priors ###
  for(pop in 1:Npops){
    prod[pop] ~ dlnorm(logProdMu,logProdTau)
    cap[pop] ~ dlnorm(log(capSlope*habVar[pop]),logCapTau)
    fryAdj[pop] ~ dlnorm(logAdjMu,logAdjTau)
    
    winterSurvPopL[pop] ~ dnorm(winterSurvMu, winterSurvTau)
    winterSurvPopTau[pop] ~ dgamma(0.001,0.001)
    oceanSurvPopL[pop] ~ dnorm(oceanSurvMu, oceanSurvTau)
    oceanSurvPopTau[pop] ~ dgamma(0.001,0.001)

    SRresidTau[pop] ~ dgamma(0.001,0.001)
    SRresidSD[pop] <- 1.0/sqrt(SRresidTau[pop])
    SRFresidTau[pop] ~ dgamma(0.001,0.001)
    SRFresidSD[pop] <- 1.0/sqrt(SRFresidTau[pop])
   }

  ### Hyper-priors (i.e. priors describing the distributions of parameters that vary by population)
  # productivity
  logProdMu ~ dnorm(prodMuPrior[1],prodMuPrior[2])
  logProdSD ~ dt(0,1,1)T(0,) # half cauchy with var=tau=sd=1 
  logProdTau <- 1.0/(logProdSD*logProdSD)
  pLeave <- 0.1 # a small proportion of fry that leave independent of density.
  
  # capacity
  capSlope ~ dlnorm(capSlopePrior[1],capSlopePrior[2])
  logCapSD ~ dt(0,1,1)T(0,)  # half cauchy with var=tau=sd=1 # dunif(0,10) #
  logCapTau <- 1.0/(logCapSD*logCapSD)
  
  # fry winter survival adjustment
  logAdjMu ~ dnorm(-1,0.001)
  logAdjSD ~ dt(0,1,1)T(0,) # half cauchy with var=tau=sd=1 
  logAdjTau <- 1.0/(logAdjSD*logAdjSD)
  
  # winter survival
  winterSurvMu ~ dnorm(0,0.001)
  winterSurvSD ~ dt(0,1,1)T(0,)  # half cauchy with var=tau=sd=1
  winterSurvTau <- pow(winterSurvSD,-2)
  
  # ocean survival
  oceanSurvMu ~ dnorm(oceanSurvMuPrior[1],oceanSurvMuPrior[2])
  oceanSurvSD ~ dt(0,1,1)T(0,)  # half cauchy with var=tau=sd=1
  oceanSurvTau <- pow(oceanSurvSD,-2)

  ### year effects (SR resids), smolt residuals and ocean survival common to all populations
  for(k in 1:Nyears){
    yearEffectTmp[k] ~ dnorm(0,yearEffectTau)
    yearEffect[k] <- yearEffectTmp[k]-mean(yearEffectTmp)
    yearEffectTmpOS[k] ~ dnorm(0,yearEffectTauOS)
    yearEffectOS[k] <- yearEffectTmpOS[k]-mean(yearEffectTmpOS)
  }
  yearEffectTau ~ dgamma(0.001,0.001)
  yearEffectTauOS ~ dgamma(0.001,0.001)


  #######################################
  ########### OBSERVATION MODEL #########
  #######################################

  # smolt data
  for(i in 1:Nsmolt){ # smolt trap count (spring)
    smoltObs[i] ~ dlnorm(log(smoltRes[smoltInd[i]]),smoltObsTau)
  }
  
  # fry data
  for(i in 1:Nfry){ # fry trap count (spring)
    fryObs[i] ~ dlnorm(log(fry[fryInd[i]]),fryObsTau)
  }
    
  # escapement data
  for(i in 1:Nesc){
    escapementObs[i] ~ dlnorm(log(spawners[escInd[i]]),escObsTau)
  }

  smoltObsTau ~ dgamma(0.001,0.001)
  fryObsTau ~ dgamma(0.001,0.001)
  escObsTau ~ dgamma(0.001,0.001)

 }

